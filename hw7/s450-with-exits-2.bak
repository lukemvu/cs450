;#lang r5rs
(define call/cc call-with-current-continuation)
;;; file: s450.scm
;;;
;;; Metacircular evaluator from chapter 4 of STRUCTURE AND
;;; INTERPRETATION OF COMPUTER PROGRAMS (2nd edition)
;;;
;;; Modified by kwn, 3/4/97
;;; Modified and commented by Carl Offner, 10/21/98 -- 10/12/04
;;;
;;; This code is the code for the metacircular evaluator as it appears
;;; in the textbook in sections 4.1.1-4.1.4, with the following
;;; changes:
;;;
;;; 1.  It uses #f and #t, not false and true, to be Scheme-conformant.
;;;
;;; 2.  Some function names were changed to avoid conflict with the
;;; underlying Scheme:
;;;
;;;       eval => xeval
;;;       apply => xapply
;;;       extend-environment => xtend-environment
;;;
;;; 3.  The driver-loop is called s450.
;;;
;;; 4.  The booleans (#t and #f) are classified as self-evaluating.
;;;
;;; 5.  These modifications make it look more like UMB Scheme:
;;;
;;;        The define special form evaluates to (i.e., "returns") the
;;;          variable being defined.
;;;        No prefix is printed before an output value.
;;;
;;; 6.  I changed "compound-procedure" to "user-defined-procedure".
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	 xeval and xapply -- the kernel of the metacircular evaluator
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Initialize the special-form-table
(define (make-table)
  (list 'special-form-table))
(define special-form-table (make-table))

; Define lookup--lookup-action returns action OR #f
(define (lookup key table)
  (let ((record (assoc key (cdr table))))
    (if record
        (cdr record)
        #f)))
(define (lookup-action key)
  (lookup key special-form-table))

; Define special form installations
(define (insert! key value table)
  (let ((record (assoc key (cdr table))))
    (if record
        (set-cdr! record value)
        (set-cdr! table
                  (cons (cons key value) (cdr table)))))
  (display key))
(define (install-special-form key value)
  (if (member key (caar the-global-environment))
      (display "ERROR--giving a defined name to a new special form")
      (cond ((not (lookup-action key))
             (insert! key value special-form-table)
             (newline))
            (else (display "ERROR--reinstalling a special form")
                  (newline)))))

; check type?
(define (type-of exp)
  (if (pair? exp)
      (car exp)
      exp))

; special form generated message
(define (prompt-special-form action)
  (display "Special form: ")
  action)

;XEVAL
(define (xeval exp env)
  (let ((action (lookup-action (type-of exp))))
    ; does special form exist in sft?
    (if (not (false? action))
        ;then does it have its arguments?
        (if (pair? exp)
            ;then execute
            (action exp env)
            ;else do not execute
            (prompt-special-form exp))
        ;else not a special form
        (cond ((self-evaluating? exp) exp)
              ((variable? exp) (lookup-variable-value exp env))
              ((application? exp)
               (xapply (xeval (operator exp) env)
                       (list-of-values (operands exp) env)))
              (else
               ;(error "Unknown expression type -- XEVAL " exp)
               (if (eof-object? exp)
                   (exit)
                   (s450error "Unknown expression type -- XEVAL " exp))
               )))))

; XAPPLY
(define (xapply procedure arguments)
  (cond ((primitive-procedure? procedure)
         (apply-primitive-procedure procedure arguments))
        ((user-defined-procedure? procedure)
         (eval-sequence
           (procedure-body procedure)
           (xtend-environment
             (procedure-parameters procedure)
             arguments
             (procedure-environment procedure))))
        (else
         ;(error "Unknown procedure type -- XAPPLY " procedure)
         (s450error "Unknown procedure type -- XAPPLY  " procedure)
         )))

;;; Handling procedure arguments
(define (list-of-values exps env)
  (if (no-operands? exps)
      '()
      (cons (xeval (first-operand exps) env)
            (list-of-values (rest-operands exps) env))))

;;; These functions, called from xeval, do the work of evaluating some
;;; of the special forms:

(define (eval-if exp env)
  (if (true? (xeval (if-predicate exp) env))
      (xeval (if-consequent exp) env)
      (xeval (if-alternative exp) env)))

(define (eval-sequence exps env)
  (cond ((last-exp? exps) (xeval (first-exp exps) env))
        (else (xeval (first-exp exps) env)
              (eval-sequence (rest-exps exps) env))))

(define (eval-assignment exp env)
  (if (not (lookup-action (cadr exp)))
      (let ((name (assignment-variable exp)))
        (set-variable-value! name
                             (xeval (assignment-value exp) env)
                             env)
        name)
      "ERROR--reassigning a special form name"))

(define (eval-definition exp env)
  (if (not (lookup-action (cadr exp)))
      (let ((name (definition-variable exp)))  
        (define-variable! name
          (xeval (definition-value exp) env)
          env)
        name)
      "ERROR--defining a special form name"))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	 Representing expressions
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; Numbers, strings, and booleans are all represented as themselves.
;;; (Not characters though; they don't seem to work out as well
;;; because of an interaction with read and display.)

(define (self-evaluating? exp)
  (or (number? exp)
      (string? exp)
      (boolean? exp)
      ))

;;; variables -- represented as symbols

(define (variable? exp) (symbol? exp))

;;; quote -- represented as (quote <text-of-quotation>)

(define (quoted? exp)
  (tagged-list? exp 'quote))

(define (text-of-quotation exp) (cadr exp))

(define (tagged-list? exp tag)
  (if (pair? exp)
      (eq? (car exp) tag)
      #f))

;;; assignment -- represented as (set! <var> <value>)

(define (assignment? exp) 
  (tagged-list? exp 'set!))

(define (assignment-variable exp) (cadr exp))

(define (assignment-value exp) (caddr exp))

;;; definitions -- represented as
;;;    (define <var> <value>)
;;;  or
;;;    (define (<var> <parameter_1> <parameter_2> ... <parameter_n>) <body>)
;;;
;;; The second form is immediately turned into the equivalent lambda
;;; expression.

(define (definition? exp)
  (tagged-list? exp 'define))

(define (definition-variable exp)
  (if (symbol? (cadr exp))
      (cadr exp)
      (caadr exp)))

(define (definition-value exp)
  (if (symbol? (cadr exp))
      (caddr exp)
      (make-lambda (cdadr exp)
                   (cddr exp))))

;;; lambda expressions -- represented as (lambda ...)
;;;
;;; That is, any list starting with lambda.  The list must have at
;;; least one other element, or an error will be generated.

(define (lambda? exp) (tagged-list? exp 'lambda))

(define (lambda-parameters exp) (cadr exp))
(define (lambda-body exp) (cddr exp))

(define (make-lambda parameters body)
  (cons 'lambda (cons parameters body)))

;;; conditionals -- (if <predicate> <consequent> <alternative>?)

(define (if? exp) (tagged-list? exp 'if))

(define (if-predicate exp) (cadr exp))

(define (if-consequent exp) (caddr exp))

(define (if-alternative exp)
  (if (not (null? (cdddr exp)))
      (cadddr exp)
      #f))

(define (make-if predicate consequent alternative)
  (list 'if predicate consequent alternative))


;;; sequences -- (begin <list of expressions>)

(define (begin? exp) (tagged-list? exp 'begin))

(define (begin-actions exp) (cdr exp))

(define (last-exp? seq) (null? (cdr seq)))
(define (first-exp seq) (car seq))
(define (rest-exps seq) (cdr seq))

(define (sequence->exp seq)
  (cond ((null? seq) seq)
        ((last-exp? seq) (first-exp seq))
        (else (make-begin seq))))

(define (make-begin seq) (cons 'begin seq))


;;; procedure applications -- any compound expression that is not one
;;; of the above expression types.

(define (application? exp) (pair? exp))
(define (operator exp) (car exp))
(define (operands exp) (cdr exp))

(define (no-operands? ops) (null? ops))
(define (first-operand ops) (car ops))
(define (rest-operands ops) (cdr ops))


;;; Derived expressions -- the only one we include initially is cond,
;;; which is a special form that is syntactically transformed into a
;;; nest of if expressions.

(define (cond? exp) (tagged-list? exp 'cond))

(define (cond-clauses exp) (cdr exp))

(define (cond-else-clause? clause)
  (eq? (cond-predicate clause) 'else))

(define (cond-predicate clause) (car clause))

(define (cond-actions clause) (cdr clause))

(define (cond->if exp)
  (expand-clauses (cond-clauses exp)))

(define (expand-clauses clauses)
  (if (null? clauses)
      #f                          ; no else clause -- return #f
      (let ((first (car clauses))
            (rest (cdr clauses)))
        (if (cond-else-clause? first)
            (if (null? rest)
                (sequence->exp (cond-actions first))
                ;(error "ELSE clause isn't last -- COND->IF "clauses)
                (s450error "ELSE clause isn't last -- COND->IF " clauses)
                )
            (make-if (cond-predicate first)
                     (sequence->exp (cond-actions first))
                     (expand-clauses rest))))))

;; special form load
(define eval-load
  (lambda (exp env)
    (define (filename exp) (cadr exp))
    (define thunk (lambda ()
                    (readfile)
                    ))
    (define readfile (lambda()
                       (let ((item (read)))
                         (if (not (eof-object? item))
                             (begin
                               (xeval item env)
                               (readfile))))
                       ))
    (with-input-from-file (filename exp) thunk)
    (filename exp)      ; return the name of the file - why not?
    ))
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	 Truth values and procedure objects
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; Truth values

(define (true? x)
  (not (eq? x #f)))

(define (false? x)
  (eq? x #f))


;;; Procedures

(define (make-procedure parameters body env)
  (list 'procedure parameters body env))

(define (user-defined-procedure? p)
  (tagged-list? p 'procedure))


(define (procedure-parameters p) (cadr p))
(define (procedure-body p) (caddr p))
(define (procedure-environment p) (cadddr p))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	 Representing environments
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; An environment is a list of frames.

(define (enclosing-environment env) (cdr env))

(define (first-frame env) (car env))

(define the-empty-environment '())

;;; Each frame is represented as a pair of lists:
;;;   1.  a list of the variables bound in that frame, and
;;;   2.  a list of the associated values.

(define (make-frame variables values)
  (cons variables values))

(define (frame-variables frame) (car frame))
(define (frame-values frame) (cdr frame))

(define (add-binding-to-frame! var val frame)
  (set-car! frame (cons var (car frame)))
  (set-cdr! frame (cons val (cdr frame))))

;;; Extending an environment

(define (xtend-environment vars vals base-env)
  (if (= (length vars) (length vals))
      (cons (make-frame vars vals) base-env)
      (if (< (length vars) (length vals))
          ;(error "Too many arguments supplied " vars vals)
          ;(error "Too few arguments supplied " vars vals)
          (s450error "Too many arguments supplied " vars vals)
          (s450error "Too few arguments supplied " vars vals))))

;;; Looking up a variable in an environment

(define (lookup-variable-value var env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (car vals))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        ;(error "Unbound variable " var)
        (s450error "Unbound variable " var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))


; SPECIAL FORM 'defined?' returns #t iff <symbol> is defined in current environment
(define (defined? var env)
  (define (env-loop env)
    (define (scan vars)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((equal? var (car vars))
             (if (symbol? var)
             #t
             #f))
            (else (scan (cdr vars)))))
    (if (eq? env the-empty-environment)
        #f
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)))))
  (env-loop env))

; SPECIAL FORM 'locally-defined?' returns #t iff the <symbol> is
;               defined in the first frame of the current environment.
(define (locally-defined? var env)
  (define (scan vars)
    (cond ((null? vars)
           #f)
          ((equal? var (car vars))
           (if (symbol? var)
               #t
               #f))
          (else (scan (cdr vars)))))  
      (let ((frame (first-frame env)))
        (scan (frame-variables frame))))

; SPECIAL FORM 'make-unbound!' removes the <symbol> binding(s) from every frame
;              in the current environment.
(define (make-unbound! var env)
  (define (env-loop env)
    (define (scan prev-vars prev-vals vars vals)
      (cond ((null? vars)
             ; check next environment
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             ; unbind var
             (set-cdr! prev-vars (cdr vars))
             (set-cdr! prev-vals (cdr vals))
             (env-loop (enclosing-environment env)))
            (else
             (scan vars vals (cdr vars) (cdr vals)))))
    (if (not (eq? env the-empty-environment))
        (let* ((frame (first-frame env))
               (vars (frame-variables frame))
               (vals (frame-values frame)))
          (cond ((null? vars)
                 (display "something here"))
                ((eq? var (car vars))
                 (set-car! frame (cdr vars)) 
                 (set-cdr! frame (cdr vals)))
                (else
                 (scan vars vals (cdr vars) (cdr vals)))))))
    (env-loop env))


; SPECIAL FORM 'locally-make-unbound!' removes the <symbol> binding(s) from the
;              first frame in the current environment.
(define (locally-make-unbound! var env)
  (define (scan prev-vars prev-vals vars vals)
    (cond ((null? vars)
           (display "Var not found"))
          ((eq? var (car vars))
             (set-cdr! prev-vars (cdr vars))
             (set-cdr! prev-vals (cdr vals)))
           (else
            (scan vars vals (cdr vars) (cdr vals)))))
  (let* ((frame (first-frame env))
         (vars (frame-variables frame))
         (vals (frame-values frame)))
    (cond ((null? vars)
           (display "Var not found"))
          ((eq? var (car vars))
             (set-car! frame (cdr vars)) 
             (set-cdr! frame (cdr vals)))
          (else
           (scan vars vals (cdr vars) (cdr vals))))))

;;; Setting a variable to a new value in a specified environment.
;;; Note that it is an error if the variable is not already present
;;; (i.e., previously defined) in that environment.

(define (set-variable-value! var val env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        ;(error "Unbound variable -- SET! " var)
        (s450error "Unbound variable -- SET! " var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))

;;; Defining a (possibly new) variable.  First see if the variable
;;; already exists.  If it does, just change its value to the new
;;; value.  If it does not, define the new variable in the current
;;; frame.

(define (define-variable! var val env)
  (let ((frame (first-frame env)))
    (define (scan vars vals)
      (cond ((null? vars)
             (add-binding-to-frame! var val frame))
            ((eq? var (car vars))
             (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (scan (frame-variables frame)
          (frame-values frame))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	 The initial environment
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; This is initialization code that is executed once, when the
;;; interpreter is invoked.

(define (setup-environment)
  (let ((initial-env
         (xtend-environment the-empty-environment
                            the-empty-environment
                            the-empty-environment)))
    initial-env))

;;; Define the primitive procedures

(define (primitive-procedure? proc)
  (tagged-list? proc 'primitive))

(define (primitive-implementation proc) (cadr proc))

(define (tag-primitive action)
  (list 'primitive action))
(define (frame-names frame) (car frame))
(define (frame-actions frame) (cdr frame))

; Primitive procedure installation.
(define (install-primitive-procedure name action)
  (define (install-primitive-procedure-global name action env)
    (let ((frame (first-frame env)))
      (define (scan names actions)
        (cond ((null? names)
               ; Check primitive proc name in SFT
               (if (not (lookup-action name))
                   ; if its not there, go ahead and install into G.E.
                   (add-binding-to-frame! name (tag-primitive action) frame)
                   ; if its there, can't install it
                   (display "ERROR--installing a primitive procedure with the name of a special form")))
              ;Trying to install a primitive procedure with name of proc that already exists
              ((eq? name (car names))
               (display "No can do. Primitive procedure already exists.")
               (newline))
              (else (scan (cdr names) (cdr actions)))))
      (scan (frame-names frame)
            (frame-actions frame))))
  (install-primitive-procedure-global name action the-global-environment))


;;; Here is where we rely on the underlying Scheme implementation to
;;; know how to apply a primitive procedure.

(define (apply-primitive-procedure proc args)
  (apply (primitive-implementation proc) args))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	 EXIT PROCEDURES
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Set global var exit? to true so program knows not to loop
(define (exit)
  (set! exit? #t)
  (display "Leaving S450"))

; display error message and lists the arguments applied that failed
(define (s450error msg . args)
  (newline)
  (display msg)
  (for-each display args)
  (target))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	 The main driver loop
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; Note that (read) returns an internal representation of the next
;;; Scheme expression from the input stream.  It does NOT evaluate
;;; what is typed in -- it just parses it and returns an internal
;;; representation.  It is the job of the scheme evaluator to perform
;;; the evaluation.  In this case, our evaluator is called xeval.

(define target '())
(define exit? #f)

(define input-prompt "s450==> ")

(define (s450)
  (call/cc
   (lambda (here)
     (set! target here)))
  (prompt-for-input input-prompt)
  (let ((input (read)))
    (let ((output (xeval input the-global-environment)))
      (user-print output)))
  (if (not exit?)
      (s450)
      (set! exit? #f)))

(define (prompt-for-input string)
  (newline) (newline) (display string))

;;; Note that we would not want to try to print a representation of the
;;; <procedure-env> below -- this would in general get us into an
;;; infinite loop.

;(define (user-print object)
;  (if (user-defined-procedure? object)
;      (display (list 'user-defined-procedure
;                     (procedure-parameters object)
;                     (procedure-body object)
;                     '<procedure-env>))
;      (display object)))

(define (user-print object)
  (cond ((user-defined-procedure? object) ; if it's a user defined procedure
         (display (list 'user-defined-procedure ; print it this way
                     (procedure-parameters object)
                     (procedure-body object)
                     '<procedure-env>)))
        ((eq? (if #f #t) object) (display "")) ; void display nothing
        (else
         (display object))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	 Here we go:  define the global environment and invite the
;;;        user to run the evaluator.
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


(define the-global-environment (setup-environment))
(display "... loaded the metacircular evaluator. (s450) runs it.")
(newline)

; special form installations
(install-special-form 'lambda (lambda (exp env) (make-procedure (lambda-parameters exp)
                               (lambda-body exp)
                               env)))
(install-special-form 'begin (lambda (exp env) (eval-sequence (begin-actions exp) env)))
(install-special-form 'cond (lambda (exp env) (xeval (cond->if exp) env)))
(install-special-form 'define (lambda (exp env) (eval-definition exp env)))
(install-special-form 'quote (lambda (exp env) (text-of-quotation exp)))
(install-special-form 'set! (lambda (exp env) (eval-assignment exp env)))
(install-special-form 'if (lambda (exp env) (eval-if exp env)))
(install-special-form 'defined? (lambda (exp env) (defined? (cadr exp) env)))
(install-special-form 'locally-defined? (lambda (exp env) (locally-defined? (cadr exp) env)))
(install-special-form 'make-unbound! (lambda (exp env) (make-unbound! (cadr exp) env)))
(install-special-form 'locally-make-unbound! (lambda (exp env) (locally-make-unbound! (cadr exp) env)))
(install-special-form 'load (lambda (exp env) eval-load))

; primitive installations
(install-primitive-procedure 'car car)
(install-primitive-procedure 'cdr cdr)
(install-primitive-procedure 'cons cons)
(install-primitive-procedure 'null? null?)
(install-primitive-procedure '+ +)
(install-primitive-procedure '- -)
(install-primitive-procedure '/ /)
(install-primitive-procedure 'display display)
(install-primitive-procedure 'newline newline)
(install-primitive-procedure 'exit exit)

(define tge the-global-environment)
